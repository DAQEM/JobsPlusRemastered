//file:noinspection GroovyAssignabilityCheck
//file:noinspection DependencyNotationArgument
//file:noinspection GrDeprecatedAPIUsage
plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '2.+'
}

version = "$project.mod_version"
def config_library_version = "$project.config_library_version"
def arc_version = "$project.arc_version"
def item_restrictions_version = "$project.item_restrictions_version"

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    //noinspection GroovyAccessibility
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-fabric:${project.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    // Config library
    modImplementation "curse.maven:supermartijn642s-config-lib-438332:${project.config_library_file_fabric}"
    modImplementation "curse.maven:arc-883593:${project.arc_file_fabric}"
    modImplementation "curse.maven:item-restrictions-887774:${project.item_restrictions_file_fabric}"
    modImplementation "curse.maven:ui-933200:${project.ui_library_file_fabric}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "config_library_version", config_library_version
    inputs.property "arc_version", arc_version
    inputs.property "item_restrictions_version", item_restrictions_version
    inputs.property "ui_library_version", project.ui_library_version

    filesMatching("fabric.mod.json") {
        expand([
                "version": project.version,
                "config_library_version": config_library_version,
                "arc_version": arc_version,
                "item_restrictions_version": item_restrictions_version,
                "ui_library_version": project.ui_library_version
        ])
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "fabric"
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    //noinspection GrUnresolvedAccess
    from commonSources.archiveFile.map { zipTree(it) }
}

remapSourcesJar {
    //noinspection GroovyAccessibility
    archiveClassifier = "fabric-sources"
}

//noinspection GrUnresolvedAccess
components.java {
    //noinspection GrUnresolvedAccess
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        //noinspection GrUnresolvedAccess
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            //noinspection GrUnresolvedAccess
            artifactId = rootProject.archives_base_name + "-" + project.name
            //noinspection GrUnresolvedAccess
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

curseforge {
    apiKey = System.getenv('CURSEFORGE_API_KEY')
    project {
        id = project.curse_forge_project_id
        releaseType = project.curse_forge_release_type
        changelogType = "markdown"
        changelog = rootProject.file('changelog.md')
        addGameVersion project.minecraft_version
        addGameVersion "Java 17"
        addGameVersion "Fabric"

        relations {
            requiredDependency("architectury-api")
            requiredDependency("arc")
            requiredDependency("ui")
            requiredDependency("item-restrictions")
            requiredDependency("supermartijn642s-config-lib")
        }

        mainArtifact(remapJar) {
            displayName = "Jobs+ Fabric $project.minecraft_version - $project.mod_version"
        }

        addArtifact(remapSourcesJar) {
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

afterEvaluate {
    tasks.curseforge916985.dependsOn remapJar
}

//modrinth {
//    token = System.getenv("MODRINTH_API_KEY")
//    projectId = "hxGLlCnq"
//    versionName = "Jobs+ Fabric $rootProject.minecraft_version - $rootProject.mod_version"
//    versionNumber = "$rootProject.mod_version"
//    versionType = "$rootProject.curse_forge_release_type"
//    uploadFile = remapJar
//    additionalFiles = [remapSourcesJar]
//    gameVersions = ["$rootProject.minecraft_version"]
//    loaders = ["fabric"]
//    changelog = rootProject.file('changelog.md').text
//    dependencies {
//        required.project "architectury-api"
//        required.project "arc"
//        required.project "item-restrictions"
//        required.project "supermartijn642s-config-lib"
//    }
//}