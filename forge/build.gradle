//file:noinspection GroovyAssignabilityCheck
//file:noinspection GrDeprecatedAPIUsage
//file:noinspection DependencyNotationArgument
plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

version = "$project.mod_version"
def config_library_version = "$project.config_library_version"
def arc_version = "$project.arc_version"
def item_restrictions_version = "$project.item_restrictions_version"

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    //noinspection GroovyAccessibility
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        //noinspection GrUnresolvedAccess
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "jobsplus-common.mixins.json"
        mixinConfig "jobsplus.mixins.json"
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    forge "net.minecraftforge:forge:${project.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-forge:${project.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    // Config library
    modImplementation "curse.maven:supermartijn642s-config-lib-438332:${project.config_library_file_forge}"
    modImplementation "curse.maven:arc-883593:${project.arc_file_forge}"
    modImplementation "curse.maven:item-restrictions-887774:${project.item_restrictions_file_forge}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "config_library_version", config_library_version
    inputs.property "arc_version", arc_version
    inputs.property "item_restrictions_version", item_restrictions_version

    filesMatching("META-INF/mods.toml") {
        expand([
                "version": project.version,
                "config_library_version": config_library_version,
                "arc_version": arc_version,
                "item_restrictions_version": item_restrictions_version
        ])
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "forge"
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    //noinspection GrUnresolvedAccess
    from commonSources.archiveFile.map { zipTree(it) }
}

//noinspection GrUnresolvedAccess
components.java {
    //noinspection GrUnresolvedAccess
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        //noinspection GrUnresolvedAccess
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            //noinspection GrUnresolvedAccess
            artifactId = rootProject.archives_base_name + "-" + project.name
            //noinspection GrUnresolvedAccess
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}


curseforge {
    apiKey = System.getenv("CURSEFORGE_API_KEY")
    project {
        id = project.curse_forge_project_id
        releaseType = project.curse_forge_release_type
        changelogType = "markdown"
        changelog = rootProject.file('changelog.md')
        addGameVersion project.minecraft_version
        addGameVersion "Java 17"
        addGameVersion "Forge"

        relations {
            requiredDependency("architectury-api")
            requiredDependency("item-restrictions")
            requiredDependency("supermartijn642s-config-lib")
        }

        mainArtifact(remapJar) {
            displayName = "Jobs+ Forge $project.minecraft_version - $project.mod_version"
        }

        addArtifact(sourcesJar) {
        }
    }
}